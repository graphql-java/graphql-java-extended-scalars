import java.text.SimpleDateFormat


plugins {
    id 'java'
    id 'groovy'
    id 'java-library'
    id 'maven'
    id 'maven-publish'
    id 'signing'
    id "io.github.gradle-nexus.publish-plugin" version "1.0.0"
    id "biz.aQute.bnd.builder" version "6.2.0"
}

def getDevelopmentVersion() {
    def output = new StringBuilder()
    def error = new StringBuilder()
    def gitShortHash = "git -C ${projectDir} rev-parse --short HEAD".execute()
    gitShortHash.waitForProcessOutput(output, error)
    def gitHash = output.toString().trim()
    if (gitHash.isEmpty()) {
        println "git hash is empty: error: ${error.toString()}"
        throw new IllegalStateException("git hash could not be determined")
    }
    new SimpleDateFormat('yyyy-MM-dd\'T\'HH-mm-ss').format(new Date()) + "-" + gitHash
}


def releaseVersion = System.env.RELEASE_VERSION
version = releaseVersion ? releaseVersion : getDevelopmentVersion()
println "Building version = " + version
group = 'com.graphql-java'

if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
    def msg = String.format("This build must be run with java 1.8 - you are running %s - gradle finds the JDK via JAVA_HOME=%s",
            JavaVersion.current(), System.getenv("JAVA_HOME"))
    throw new GradleException(msg)
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
}


dependencies {
    compile "com.graphql-java:graphql-java:19.2"

    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    testImplementation('org.codehaus.groovy:groovy:2.5.13')

}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId group
            artifactId 'graphql-java-extended-scalars'
            version version

            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'graphql-java-extended-scalars'
                    description 'A library fo extended scalars for graphql-java'
                    url 'https://github.com/graphql-java/graphql-java-extended-scalars'
                    inceptionYear '2018'

                    scm {
                        url 'https://github.com/graphql-java/graphql-java-extended-scalars'
                        connection 'scm:git@github.com:graphql-java/graphql-java-extended-scalars.git'
                        developerConnection 'scm:git@github.com:graphql-java/graphql-java-extended-scalars.git'
                    }

                    licenses {
                        license {
                            name 'MIT'
                            url 'https://github.com/graphql-java/graphql-java/blob/master/LICENSE.md'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'bbakerman'
                            name 'Brad Baker'
                            email 'bbakerman@gmail.com'
                        }
                    }
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = System.env.MAVEN_CENTRAL_USER
            password = System.env.MAVEN_CENTRAL_PASSWORD
        }
    }
}

signing {
    def signingKey = System.env.MAVEN_CENTRAL_PGP_KEY
    useInMemoryPgpKeys(signingKey, "")
    sign publishing.publications
}


// all publish tasks depend on the build task
tasks.withType(PublishToMavenRepository) {
    dependsOn build
}

jar {
    manifest {
        attributes('Automatic-Module-Name': 'graphql-java-extended-scalars',
                '-exportcontents': 'graphql.scalars.*',
                '-removeheaders': 'Private-Package')
    }
}
